buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE")
    }
}

project.ext {
    httpClientVersion = "4.5.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

sourceSets.main.java.srcDirs += "${buildDir}/generated-sources/jaxb"

// tag::xsd[]
task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.schema = "src/main/resources/translator.xsd"

	inputs.file ext.schema
    outputs.dir ext.sourcesDir

    doFirst() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }
        
    }
}
// end::xsd[]

task afterEclipseImport {
	dependsOn "genJaxb"
}

compileJava.dependsOn genJaxb


// tag::jaxb[]
configurations {
    jaxb
}


repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.springframework.boot:spring-boot-starter-ws"
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    compile "wsdl4j:wsdl4j:1.6.1"
//  compile files(genJaxb.classesDir).builtBy(genJaxb)

    jaxb 	"com.sun.xml.bind:jaxb-xjc:2.2.4-1"

    testCompile "org.springframework.boot:spring-boot-starter-test"
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}